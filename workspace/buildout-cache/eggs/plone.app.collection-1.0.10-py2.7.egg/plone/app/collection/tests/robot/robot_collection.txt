*** Settings ***

Library  Selenium2Library  timeout=10  implicit_wait=0.5

Resource  Products/CMFPlone/tests/robot/keywords.txt

Variables  plone/app/testing/interfaces.py
Variables  Products/CMFPlone/tests/robot/variables.py

Suite Setup  Suite Setup
Suite Teardown  Suite Teardown


*** Test Cases ***

Short name (id) Criterion
    Given a document  First Document
      and a document  Second Document
      and a collection  My Collection
     When I set the 'Short name (id)' Criterion to 'first-document'
     Then Page should contain  First Document
      and Page should not contain  Second Document


*** Keywords ***

Suite Setup
    Open browser  ${TEST_FOLDER}
    Given a site owner

Suite Teardown
    Close All Browsers


# --- WHEN -------------------------------------------------------------------

I set the '${criterion}' Criterion to '${value}'
    Click Link  Edit
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  ${criterion}
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select From List  xpath=//select[@class='queryoperator']  Is
    Input Text  name=query.v:records  ${value}
    Wait Until Page Contains  1 items matching your search terms.
    Click Button  Save
